group 'ignite-project'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'docker'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8

ext {
    igniteVersion = '2.2.0'
    flywayVersion = '4.2.0'
    guiceVersion = '4.1.0'
    jacksonVersion = '2.9.0'
    logbackClassicVersion = '1.2.2'
    rapidoidVersion = '5.4.4'
    typesafeConfigVersion = '1.3.1'
    mockitoVersion = '1.10.19'
    junitVersion = '4.12'
}

dependencies {
    compile "org.apache.ignite:ignite-core:$igniteVersion"

    compile "com.google.inject:guice:$guiceVersion"
    compile "com.google.inject.extensions:guice-multibindings:$guiceVersion"
    compile "com.typesafe:config:$typesafeConfigVersion"
    compile "org.flywaydb:flyway-core:$flywayVersion"
    compile "org.rapidoid:rapidoid-http-server:$rapidoidVersion"
    compile "ch.qos.logback:logback-classic:$logbackClassicVersion"

    testCompile "junit:junit:$junitVersion"
    testCompile("org.mockito:mockito-all:$mockitoVersion")
}

shadowJar() {
    mergeServiceFiles()
    exclude 'META-INF/.SF'
    exclude 'META-INF/.DSA'
    exclude 'META-INF/.RSA'
    manifest {
        attributes 'Main-Class': 'com.shaad.ignite.Main'
    }
}

build.dependsOn shadowJar
task buildDocker(type: Docker) {
    dependsOn build
    push = false
    applicationName = 'ignite'
    dockerfile = file('etc/docker/Dockerfile')
    doFirst {
        copy {
            from 'build/libs/ignite-' + version + '-all.jar'
            into stageDir
        }
    }
}